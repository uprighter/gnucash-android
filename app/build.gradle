import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'

def versionMajor = 2
def versionMinor = 4
def versionPatch = 0
def versionBuild = 3

static def buildTime() {
    def df = new SimpleDateFormat("yyyyMMdd HH:mm 'UTC'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}


android {
    compileSdk 34
    defaultConfig {
        applicationId "org.gnucash.android"
        testApplicationId 'org.gnucash.android.test'
        minSdkVersion 21
        targetSdkVersion 33
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resValue "string", "app_version_name", "${versionName}"
        resValue "string", "app_minor_version", "${versionMinor}"
        buildConfigField "boolean", "CAN_REQUEST_RATING", "false"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

        testInstrumentationRunner "org.gnucash.android.test.ui.util.GnucashAndroidTestRunner"

    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "GnucashAndroid_v${variant.versionName}.apk"
        }
    }

    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
        }

        if (project.hasProperty("RELEASE_STORE_FILE")) {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        //todo re-enable proguard and test coverage
        release {
//            minifyEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            debuggable true
            minifyEnabled false
//            testCoverageEnabled true
            signingConfig signingConfigs.debug
        }
    }
    lint {
        abortOnError false
    }

    flavorDimensions "stability"

    productFlavors {
        development {
            applicationId 'org.gnucash.android.devel'
            testApplicationId 'org.gnucash.android.devel.test'
            resValue "string", "app_name", "GnuCash-devel"
            versionName "${versionMajor}.${versionMinor}.${versionPatch}-dev${versionBuild}_r${gitSha()}"
            resValue "string", "app_version_name", "${versionName}"

            resValue "string", "dropbox_app_key", "dhjh8ke9wf05948"
            resValue "string", "dropbox_app_secret", "h2t9fphj3nr4wkw"
            resValue "string", "manifest_dropbox_app_key", "db-dhjh8ke9wf05948"
        }

        beta {
            resValue "string", "app_name", "GnuCash - beta"
            versionName "${versionMajor}.${versionMinor}.${versionPatch}-beta${versionBuild}"
            resValue "string", "app_version_name", "${versionName}"

            if (project.hasProperty("RELEASE_DROPBOX_APP_KEY")) {
                resValue "string", "dropbox_app_key", RELEASE_DROPBOX_APP_KEY
                resValue "string", "dropbox_app_secret", RELEASE_DROPBOX_APP_SECRET
                resValue "string", "manifest_dropbox_app_key", "db-${RELEASE_DROPBOX_APP_KEY}"
            } else {
                resValue "string", "dropbox_app_key", "dhjh8ke9wf05948"
                resValue "string", "dropbox_app_secret", "h2t9fphj3nr4wkw"
                resValue "string", "manifest_dropbox_app_key", "db-dhjh8ke9wf05948"
            }
        }

        production {
            resValue "string", "app_name", "GnuCash"
            buildConfigField "boolean", "CAN_REQUEST_RATING", "true"

            if (project.hasProperty("RELEASE_DROPBOX_APP_KEY")) {
                resValue "string", "dropbox_app_key", RELEASE_DROPBOX_APP_KEY
                resValue "string", "dropbox_app_secret", RELEASE_DROPBOX_APP_SECRET
                resValue "string", "manifest_dropbox_app_key", "db-${RELEASE_DROPBOX_APP_KEY}"
            } else {
                resValue "string", "dropbox_app_key", "dhjh8ke9wf05948"
                resValue "string", "dropbox_app_secret", "h2t9fphj3nr4wkw"
                resValue "string", "manifest_dropbox_app_key", "db-dhjh8ke9wf05948"
            }
        }
    }

    bundle {
        language {
            // This is disabled so that the App Bundle does NOT split the APK for each language.
            // We're gonna use the same APK for all languages.
            enableSplit false
        }
    }

    compileOptions { //we want switch with strings during xml parsing
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    namespace 'org.gnucash.android'

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

afterEvaluate {
    apply plugin: 'spoon'
    spoon {
        debug = true
        grantAllPermissions = true
        codeCoverage = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.21"

    // Jetpack
    implementation(
            'androidx.appcompat:appcompat:1.6.1',
            'com.google.android.material:material:1.11.0',
            'androidx.cardview:cardview:1.0.0',
            'androidx.preference:preference:1.2.0',
            'androidx.recyclerview:recyclerview:1.3.0'
    )

    implementation 'net.objecthunter:exp4j:0.4.7'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'

    implementation 'com.google.android.gms:play-services-drive:17.0.0'

    // Logging
    implementation 'com.google.firebase:firebase-crashlytics:18.6.1'
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Dependency Injection
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

    implementation('com.uservoice:uservoice-android-sdk:1.2.10') {
        exclude module: 'commons-logging'
        exclude module: 'httpcore'
        exclude module: 'httpclient'
    }

    implementation 'org.jraf:android-switch-backport:2.0.1@aar'
    implementation 'com.github.nextcloud:android-library:1.0.31'
    implementation 'com.squareup:android-times-square:1.6.5@aar'
    implementation 'com.github.PhilJay:MPAndroidChart:2.2.4'

    implementation 'joda-time:joda-time:2.9.4'
    implementation 'org.apache.jackrabbit:jackrabbit-webdav:2.13.3'
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
    implementation 'com.github.techfreak:wizardpager:1.0.3'
    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.3'
    implementation 'io.github.kobakei:ratethisapp:1.1.3'

    debugImplementation 'com.facebook.stetho:stetho:1.5.0'

    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.22.0'
}

def androidEspressoVersion = "3.5.1"
dependencies {
    //the following are only added so that the app and test version both us the same versions
    androidTestImplementation 'com.google.android.material:material:1.11.0'

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:' + androidEspressoVersion
    androidTestImplementation 'androidx.test.espresso:espresso-intents:' + androidEspressoVersion
    androidTestImplementation('androidx.test.espresso:espresso-contrib:' + androidEspressoVersion) {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    androidTestImplementation('com.squareup.assertj:assertj-android:1.1.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation 'com.squareup.spoon:spoon-client:1.6.4'
}
